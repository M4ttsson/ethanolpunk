; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Äthyl"
#define MyAppVersion "0.1A"
#define MyAppPublisher "SKIC"
#define MyAppExeName "Athyl.exe"

;location for redist.
#define MyRedistLocation "C:\temp\Athyl setup\Redist"

#define XNARedist "xnafx40_redist.msi"
#define DotNetSetup "dotnetfx45_full_x86_x64.exe"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8CECF1A9-EA43-4FEC-8509-8E025C39A5C8}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\Athyl
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
InfoBeforeFile=C:\temp\Athyl setup\preinstall.txt
InfoAfterFile=C:\temp\Athyl setup\readme.txt
OutputDir=C:\temp\Athyl setup\Setup file
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
;prerequistes
Source: {#MyRedistLocation}\{#DotNetSetup}; DestDir: {tmp}; AfterInstall: VerifyDotNet
Source: {#MyRedistLocation}\{#XNARedist}; DestDir: {tmp}; AfterInstall: VerifyXna
;game files
Source: "C:\temp\Athyl setup\Athyl.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\temp\Athyl setup\Content\*"; DestDir: "{app}\Content"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\temp\Athyl setup\Athyl.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\temp\Athyl setup\DebugViewXNA.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\temp\Athyl setup\FarseerPhysicsXNA.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\temp\Athyl setup\NLog.dll"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: {tmp}\{#DotNetSetup}; Flags: skipifdoesntexist; Parameters: /q /noreboot; StatusMsg: Installing DotNET Framework 4.5
Filename: msiexec.exe; Parameters: " /passive /i""{tmp}\{#XNARedist}"""; StatusMsg: XNA Framework Redistributable 4.0 Refresh
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
var
  hasDotNet :Boolean;
  hasXna :Boolean;

procedure VerifyDotNet();
begin
  hasDotNet := RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\NET Framework Setup\NDP\v4');
  if hasDotNet then
    DeleteFile(ExpandConstant('{tmp}\{#DotNetSetup}'));
end;

procedure VerifyXna();
begin
  hasXna := RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\XNA\Framework\v4.0 Refresh') or RegKeyExists(HKEY_LOCAL_MACHINE, 'Software\Wow6432Node\Microsoft\XNA\Framework\v4.0');
  if hasXna then
    DeleteFile(ExpandConstant('{tmp}\{#XNARedist}'));
end;
